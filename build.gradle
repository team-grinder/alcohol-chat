plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
	id "org.sonarqube" version "5.0.0.4638"
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.github.f4b6a3:uuid-creator:5.3.2'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor 'com.querydsl:querydsl-apt'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.0.0'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
}

sonar {
	properties {
		property "sonar.projectKey", "team-grinder_alcohol-chat"
		property "sonar.organization", "team-grinder"
		property "sonar.host.url", "https://sonarcloud.io/"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

// QueryDSL 설정
// sourceSets 섹션에서 main 소스셋에 생성된 Q타입 클래스 경로를 추가
def querydslSrcDir = 'build/generated/querydsl'
sourceSets {
	main {
		java {
			srcDirs += [querydslSrcDir]
		}
	}
}
// compileJava 태스크에 컴파일러 옵션을 추가하여 QueryDSL이 생성한 Q타입 클래스에 @Generated 애노테이션을 추가. 생성된 Q타입 클래스가 포함된 디렉토리가 Java 소스 디렉토리에 포함된다.
compileJava {
	options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=javax.annotation.Generated'
}
// 모든 JavaCompile 태스크에 대해 generatedSourceOutputDirectory를 build/generated로 설정. Q타입 클래스가 지정된 디렉토리에 생성된다.
tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = file(querydslSrcDir)
}
// clean 태스크가 실행될 때 build/generated 디렉토리를 삭제하도록 설정
clean {
	delete file(querydslSrcDir)
}
